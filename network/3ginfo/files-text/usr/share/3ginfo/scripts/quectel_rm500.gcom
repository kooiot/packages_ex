opengt
 set com 115200n81
 set comecho off
 set senddelay 0.02
 waitquiet 0.2 0.2

 send "AT\^CURC=0^m"
 waitfor 5 "OK","ERROR"

 let $c="AT+CSQ^m"
 let $r="+CSQ"
 gosub readatcmd

 let $c="AT+CCID^m"
 let $r="+CCID"
 gosub readatcmd

 let $c="ATE1;+CGMI^m"
 gosub readatcmdnr
 let $v=$s

 let $c='AT+QENG="servingcell"^m'
 gosub getresult

 let $c="AT+QCAINFO^m"
 gosub getresult

 let $c="AT+QNWINFO^m"
 gosub getresult

 let $c="AT\^SYSINFO^m"
 let $r="\^SYSINFO"
 gosub readatcmd

 let $c="AT+COPS=3,2;+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

 let $c="AT+CREG=2;+CREG?^m"
 let $r="+CREG"
 gosub readatcmd

 let $c="AT+CGREG=2;+CGREG?^m"
 let $r="+CGREG"
 gosub readatcmd

 let $c="AT+C5GREG=2;+C5GREG?^m"
 let $r="+C5GREG"
 gosub readatcmd

 let $c="AT+CEREG=2;+CEREG?^m"
 let $r="+CEREG"
 gosub readatcmd

 let $c="AT+CGEQNEG=1^m"
 let $r="+CGEQNEG"
 gosub readatcmd

 let $c="ATE1;+CGMM^m"
 let $r="+CGMM"
 gosub readatcmd
 print "\nDEVICE:",$v," ",$mid($s, 8, 32),"\n"

 exit 0

:readatcmd
 let i=15
 send $c
:loop
 get 0.1 "^m" $s
 let l=len($r)
 if len($s) < l goto loop1
 if $mid($s,1,l) <> $r goto loop1
 print $s
 return

:loop1
 if len($s) < 2 goto loop2
 if $mid($s,1,2) = "ER" return
 if $mid($s,1,2) = "CO" return
:loop2
 if i = 0 return
 let i=i-1
 goto loop

:readatcmdnr
 let i=15
 send $c
:loop3
 get 0.1 "^m" $s
 if len($s) < 2 goto loop5
 if $mid($s,1,2) = "ER" goto loop4
 if $mid($s,1,2) = "OK" goto loop5
 if $mid($s,1,2) = "AT" goto loop5
 if $mid($s,1,2) = "TE" goto loop5
 if $mid($s,1,1) = "+"  goto loop5
 if $mid($s,1,1) = "\^"  goto loop5
 let l=len($s)
 let $s=$mid($s,1,l)
 return

:getresult
 let t=time()+25
 send $c
:loop6
 get 0.1 "^m" $s
 let x=len($s)
 if x=0 let $s="^mTIMEOUT ERROR"
 if $mid($s,1,2) = "AT" goto loop6
 if $s="^jOK" return
 if $mid($s,0,6)="^jERROR" return
 if $mid($s,0,8)="^jCOMMAND" return
 if $mid($s,0,11)="^j+CME ERROR" return
 if time()>t goto loop4
 if x>1 print $s
 goto loop6

:loop4
 let $s=""
 return

:loop5
 if i = 0 return
 let i=i-1
 goto loop3

